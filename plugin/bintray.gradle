apply plugin: 'maven-publish'

javadoc {
    failOnError = false
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        GnagPublication(MavenPublication) {
            from components.java
            artifactId gnagArtifactId

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    dryRun = false
    publish = true
    user = project.hasProperty('BINTRAY_USER') ? project.property('BINTRAY_USER') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('BINTRAY_API_KEY') ? project.property('BINTRAY_API_KEY') : System.getenv('BINTRAY_API_KEY')
    publications = ['GnagPublication']
    pkg {
        repo = 'maven'
        name = gnagPluginName
        desc = gnagDescription
        licenses = ['Apache-2.0']
        labels = gnagLabels
        vcsUrl = gnagGitUrl
        version {
            name = gnagPluginVersion
            vcsTag = gnagPluginVersion
            released  = new Date()
        }
    }
}

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    testRuntime files(createClasspathManifest)
}
