plugins {
    id 'maven-publish'
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.15.0"
}

apply plugin: 'groovy'
apply from: file('../version.gradle')

group 'com.btkelly'
version gnagPluginVersion

repositories {
    mavenCentral()
}

dependencies {
    //Gradle specific classes for plugin creation
    implementation gradleApi()

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation 'org.jetbrains:annotations:20.1.0'
    implementation 'com.vladsch.flexmark:flexmark-all:0.62.2'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'com.google.guava:guava:30.1.1-jre'


    // Testing
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

javadoc {
    failOnError = false
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        GnagPublication(MavenPublication) {
            from components.java
            artifactId gnagArtifactId
        }
    }
}

pluginBundle {
    website = gnagWebsite
    vcsUrl = gnagGitUrl
    tags = gnagLabels
}

gradlePlugin {
    plugins {
        gnagPlugin {
            id = gnagPackageId
            version = gnagPluginVersion
            displayName = gnagPluginName
            description = gnagDescription
            implementationClass = "com.btkelly.gnag.GnagPlugin"
        }
    }
}