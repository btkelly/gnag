buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.4"
}

allprojects {
    repositories {
        jcenter()
    }
    apply plugin: 'groovy'
    apply plugin: 'java'
    apply plugin: 'maven-publish'
}

dependencies {
    compile gradleApi()
    compile 'org.codehaus.groovy:groovy-all:2.3.6'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

group = 'com.btkelly'
version = '0.0.3'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        GnagPublication(MavenPublication) {
            from components.java
            artifactId 'gnag'

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    dryRun = false
    publish = true
    configurations = ['archives']
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_KEY')
    publications = ['GnagPublication']
    pkg {
        repo = 'maven'
        name = 'gnag-gradle-plugin'
        desc = 'Gnag GitHub pull request commenter plugin'
        licenses = ['Apache-2.0']
        labels = ['android', 'gradle', 'github']
        vcsUrl = 'https://github.com/btkelly/gnag.git'
        version {
            name = project.version
            vcsTag = project.version
            released  = new Date()
        }
    }
}