buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.6"
    id "com.github.hierynomus.license" version "0.12.1"
}

allprojects {
    repositories {
        jcenter()
    }

    apply plugin: 'java'
    apply plugin: 'maven-publish'
}

group = 'com.btkelly'
version = '0.1.4'

dependencies {

    compile gradleApi()
    compile 'javax.xml.bind:jaxb-api:2.2.12'
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.squareup.okhttp:logging-interceptor:2.7.4'
    compile 'com.github.rjeschke:txtmark:0.13'
    compile 'org.jetbrains:annotations:15.0'

    testCompile gradleTestKit()
    testCompile group: 'junit', name: 'junit', version: '4.12'

}

license {
    header = file('Apache2License.txt')
    strictCheck true
}

javadoc {
    failOnError = false
}

publishing {
    publications {
        GnagPublication(MavenPublication) {
            from components.java
            artifactId 'gnag'

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    dryRun = false
    publish = true
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_KEY')
    publications = ['GnagPublication']
    pkg {
        repo = 'maven'
        name = 'gnag-gradle-plugin'
        desc = 'Gnag GitHub pull request commenter plugin'
        licenses = ['Apache-2.0']
        labels = ['android', 'gradle', 'github']
        vcsUrl = 'https://github.com/btkelly/gnag.git'
        version {
            name = project.version
            vcsTag = project.version
            released  = new Date()
        }
    }
}

clean.dependsOn licenseFormat

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    testRuntime files(createClasspathManifest)
}